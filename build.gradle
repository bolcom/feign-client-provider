import static org.gradle.api.JavaVersion.VERSION_1_6

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply from: script('integrationTest')


compileJava {
    sourceCompatibility = VERSION_1_6
    targetCompatibility = VERSION_1_6
}

group = 'com.bol.feign'

ext {
    libraries = [:]
    versions = [:]
}

versions += [
        feign  : '6.1.1',
        slf4j  : '1.7.6',
        logback: '1.1.2',
        groovy : '2.3.8',
]

libraries += [
        groovy    : "org.codehaus.groovy:groovy:${versions.groovy}",
        loggingApi: "org.slf4j:slf4j-api:${versions.slf4j}",
]

libraries.feign = [
        "com.netflix.feign:feign-core:${versions.feign}",
        "com.netflix.feign:feign-jackson:${versions.feign}",
        "com.netflix.feign:feign-jaxrs:${versions.feign}",
]

libraries.logging = [
        "org.slf4j:jcl-over-slf4j:${versions.slf4j}",
        "ch.qos.logback:logback-classic:${versions.logback}",
]

libraries.spock = ['org.spockframework:spock-core:0.7-groovy-2.0',
                   libraries.groovy,
                   'org.objenesis:objenesis:2.0',
                   'cglib:cglib-nodep:2.2',
]

jar {
    manifest {
        attributes 'Implementation-Title': 'Feign Java Client Provider',
                'Implementation-Version': version
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile libraries.feign
    compile libraries.loggingApi

    testCompile libraries.spock
    testRuntime libraries.logging
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

File script(String name) {
    project.file("gradle/${name}.gradle")
}
